plugins {
    id 'com.github.johnrengelman.shadow' version '5.1.0' apply false
}

allprojects {
    apply plugin: 'java'
	apply plugin: 'com.github.johnrengelman.shadow'
    group = 'dev.rosewood'
    version = '1.0.0-SNAPSHOT'
	sourceCompatibility = 1.8
    targetCompatibility = 1.8
	
	repositories {
        mavenCentral()
        jcenter()

        maven { url = 'https://repo.rosewooddev.io/repository/public/' }
        maven { url = 'https://repo.aikar.co/content/groups/aikar/' }
        maven { url = 'http://maven.playpro.com' }

        maven {
            url = 'https://repo.rosewooddev.io/repository/maven-public'
            credentials {
                username project.mavenUser
                password project.mavenPassword
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
    }

    dependencies {
        shadow 'org.spigotmc:spigot:1.16.2'
    }
}

configurations {
    childJars
}

dependencies {
    implementation project(':Shared')
    implementation project(':Plugin')
	implementation project(':Jobs')
	implementation project(':McMMO')
	implementation project(':McMMOClassic')
	implementation project(':CoreProtect')

    subprojects.each {
        childJars project(it.path)
    }
}

shadowJar {
    archiveClassifier.set(null)

    relocate('dev.rosewood.rosegarden', 'dev.rosewood.rosetimber.lib.rosegarden')

    exclude 'dev/rosewood/rosegarden/lib/hikaricp/**/*.class'
    exclude 'dev/rosewood/rosegarden/lib/slf4j/**/*.class'
}

jar {
    dependsOn configurations.childJars
    from {
        configurations.childJars.collect {
            zipTree(it)
        }
    }
}

build.dependsOn shadowJar
